apply plugin: 'com.android.application'
apply from: 'util.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.ovwvwvo.wallpaper"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode appVersionCode
        versionName appVersionName

        multiDexEnabled true
    }

    dexOptions {
        preDexLibraries = true
    }

    useLibrary 'org.apache.http.legacy'

    signingConfigs {
        release {
            storeFile file('./keystore/keystore.jks')
            storePassword "$System.env.STORE_PASSWORD"
            keyAlias "$System.env.KEY_ALIAS"
            keyPassword System.getProperty("KEY_PASSWORD")
        }
        debug {
            storeFile file('./keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }

    productFlavors {
        inland {}
        foreign {
            apply plugin: 'com.google.gms.google-services'
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def fileName = output.outputFile.name;
            if (fileName != null && fileName.endsWith('.apk')) {
                fileName = fileName.replace("app", getRootProject().name + "${variant.versionName}-${variant.versionCode}")
                output.outputFile = new File(output.outputFile.parent, fileName)
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    foreignCompile 'com.google.firebase:firebase-core:11.0.2'
    foreignCompile 'com.google.firebase:firebase-database:11.0.2'
    inlandCompile 'cn.leancloud.android:avoscloud-sdk:v4.4.4'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.github.bumptech.glide:glide:4.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile 'com.ovwvwvo:jkit:0.1.7'
    compile 'com.ovwvwvo:common:0.1.7'
}